group 'com.banuba.ve.sdk.flutter.plugin.ve_sdk_flutter'
version '0.1.0'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

def ENABLE_FACE_AR = project.hasProperty("ENABLE_FACE_AR") ? project.ENABLE_FACE_AR.toBoolean() : true
def ENABLE_GALLERY = project.hasProperty("ENABLE_GALLERY") ? project.ENABLE_GALLERY.toBoolean() : true

// Below code modifies styles.xml file when Gallery is disabled
task updateGalleryStyles {
    doLast {
            println "!!! UPDATE GALLERY STYLES: ENABLED = $ENABLE_GALLERY !!!"
            def stylesFile = file('src/main/res/values/ve_styles.xml')
            if (stylesFile.exists()) {

                def galleryStyles = [
                        'galleryAlbumBlurViewStyle' : '@style/GalleryAlbumBlurViewStyle',
                        'galleryItemRadioButtonStyle' : '@style/GalleryItemRadioButtonStyle',
                        'galleryItemTextStyle' : '@style/GalleryItemTextStyle',
                        'selectedGalleryItemTextStyle' : '@style/SelectedGalleryItemTextStyle',
                        'galleryTitleTextStyle' : '@style/GalleryTitleTextStyle',
                        'galleryAlbumTitleTextStyle' : '@style/GalleryAlbumTitleTextStyle',
                        'galleryAlbumDescTextStyle' : '@style/GalleryAlbumDescTextStyle',
                        'galleryEmptyTextStyle' : '@style/GalleryEmptyTextStyle',
                        'galleryBackButtonStyle' : '@style/GalleryBackButtonStyle',
                        'galleryNextButtonStyle' : '@style/GalleryNextButtonStyle',
                        'galleryAutoCutNextButtonStyle' : '@style/GalleryAutoCutNextButtonStyle',
                        'galleryAutoCutButtonStyle' : '@style/GalleryAutoCutButtonStyle',
                        'gallerySelectionDescriptionStyle' : '@style/GallerySelectionDescriptionStyle',
                        'galleryNextParentStyle' : '@style/GalleryNextParentStyle',
                        'galleryImageViewStyle' : '@style/GalleryImageViewStyle',
                        'galleryColumnsNumber' : 'wrap_content',
                        'galleryTabLayoutStyle' : '@style/GalleryTabLayoutStyle',
                        'galleryTabTextColors' : '@color/gallery_tab_color_state_list',
                        'gallery_bg_color' : '@color/black',
                        'gallery_item_corner_radius' : '0dp',
                        'gallery_item_margin' : '0.5dp',
                        'gallery_album_divider_color' : '@color/transparent',
                        'previewActionButtonStyle' : '@style/PreviewActionButtonStyle',
                        'previewContainerStyle' : '@style/PreviewContainerStyle',
                ]

                println "# Styles file is found!"
                def content = stylesFile.getText('UTF-8')

                def pattern = /<style name="CustomIntegrationAppTheme"[^>]*>([\s\S]*?)<\/style>/
                content = content.replaceAll(pattern) { fullMatch, styleContent ->
                    def modifiedContent = styleContent
                    int totalProcessed = 0

                    galleryStyles.each { itemName, defaultValue ->
                        def itemPattern = /(\s*)<item name="${itemName}"[^>]*>([\s\S]*?)<\/item>(\s*)/

                        def itemExists = (modifiedContent =~ itemPattern).find()

                        if (ENABLE_GALLERY) {
                            if (itemExists) {
                                // Item exists - just skip (do nothing)
                                println "# Item exists, skipping: $itemName"
                            } else {
                                // Item doesn't exist - add with value
                                def newItem = "\n    <item name=\"$itemName\">$defaultValue</item>"
                                modifiedContent += newItem
                                println "# Added missing item: $itemName = $defaultValue"
                                totalProcessed++
                            }
                        } else {
                            if (itemExists) {
                                // Item exists - remove it
                                def before = modifiedContent
                                modifiedContent = modifiedContent.replaceAll(itemPattern, '')
                                if (before != modifiedContent) {
                                    println "# Removed item: $itemName"
                                    totalProcessed++
                                }
                            } else {
                                // Item doesn't exist - just skip (do nothing)
                                println "# Item doesn't exist, skipping: $itemName"
                            }
                        }
                    }

                    println "# Total items processed: $totalProcessed"
                    // Return the modified style block
                    return "<style name=\"CustomIntegrationAppTheme\" parent=\"VideoCreationTheme\">" + modifiedContent + "</style>"
                }

                // Remove multiple empty lines and clean up
                content = content.replaceAll(/(\r\n|\n){3,}/, '\n\n')
                content = content.replaceAll(/>\s+</, '>\n    <')

                // Write back to file
                stylesFile.write(content, 'UTF-8')
                println "# styles.xml has been updated successfully!"
            }
    }
}

preBuild.dependsOn updateGalleryStyles

android {
    namespace = "com.banuba.ve.sdk.flutter.plugin.ve_sdk_flutter"

    compileSdkVersion 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 36
        compileSdk 36
        buildToolsVersion "35.0.1"

        versionCode 1
        versionName "1.0"

        buildConfigField "boolean", "ENABLE_FACE_AR", "${ENABLE_FACE_AR}"
        buildConfigField "boolean", "ENABLE_GALLERY", "${ENABLE_GALLERY}"
    }

    packagingOptions {
        pickFirst '**/*.so'

        jniLibs {
            useLegacyPackaging = true
        }
    }
}

rootProject.allprojects {
    repositories {
        maven {
            name = "nexus"
            url = uri("https://nexus.banuba.net/repository/maven-releases")
        }
        maven {
            url 'https://storage.googleapis.com/download.flutter.io'
        }
        google()
        mavenCentral()
    }
}

rootProject.buildscript {
    ext.kotlin_version = '2.2.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    
    def banubaSdkVersion = '1.49.0'
    implementation "com.banuba.sdk:ffmpeg:5.3.0"
    implementation "com.banuba.sdk:camera-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:camera-ui-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:core-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:core-ui-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-flow-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-ui-sdk:${banubaSdkVersion}"

    if (ENABLE_GALLERY){
        println("!!! BANUBA GALLERY IS ENABLED !!!")
        implementation "com.banuba.sdk:ve-gallery-sdk:${banubaSdkVersion}"
    } else {
        println("!!! BANUBA GALLERY IS DISABLED !!!")
    }

    implementation "com.banuba.sdk:ve-effects-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ar-cloud:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-audio-browser-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-export-sdk:${banubaSdkVersion}"
    implementation "com.banuba.sdk:ve-playback-sdk:${banubaSdkVersion}"

    if (ENABLE_FACE_AR){
        print("!!! BANUBA FACE AR IS ENABLED !!!")
        implementation "com.banuba.sdk:effect-player-adapter:${banubaSdkVersion}"
    } else {
        print("!!! BANUBA FACE AR IS DISABLED !!!")
    }
}